export LANG=en_CA.UTF-8
export LC_ALL=en_CA.UTF-8

export ZSH=$HOME/.dotfiles
export PROJECTS=~/Projects

export VISUAL='vim'
export EDITOR=$VISUAL
export PAGER=LESS
export LESS=-r

export PATH="/usr/local/bin:$PATH"
export PATH="$HOME/bin:$PATH"
export PATH=$PATH:/Applications/Postgres.app/Contents/Versions/latest/bin

autoload -Uz up-line-or-beginning-search down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search

[[ -n "${key[Up]}"   ]] && bindkey "${key[Up]}"   up-line-or-beginning-search
[[ -n "${key[Down]}" ]] && bindkey "${key[Down]}" down-line-or-beginning-search

# stash  variables in ~/.localrc.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files
